buildscript {
    ext {
        springBootVersion = '2.0.0.M1'
        commonsLang3Version = '3.1'
        derbyVersion = '10.13.1.1'
        commonsDbcpVersion = '2.1.1'
        hibernateJpaVersion = '1.0.0.Final'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.spring.io/libs-release"}

    }
    dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.lightdiscuss'
    version = '1.0-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.spring.io/libs-release"}
    }
}


configure(subprojects.findAll { !it.name.contains('discuss-common')}) {
    dependencies {
        compile('org.springframework.boot:spring-boot-starter')
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-starter-webflux') {
            exclude module: 'spring-boot-starter-tomcat'
        }
        compile project(':discuss-common:discuss-common-utils')
        testCompile('org.springframework.boot:spring-boot-starter-test')
        compile("org.apache.commons:commons-lang3:${commonsLang3Version}")
    }

    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
    }

}

project(":discuss-server:discuss-business"){
    dependencies {
        compile project(':discuss-common:discuss-common-entity')
        compile("org.apache.derby:derby:${derbyVersion}")
        runtime("mysql:mysql-connector-java")
        compile("org.apache.commons:commons-dbcp2:${commonsDbcpVersion}")
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        compile("org.springframework.boot:spring-boot-starter-aop")
    }
}

project(":discuss-server:discuss-api"){
    dependencies {
        compile project(':discuss-server:discuss-business')
    }
}

project(":discuss-server:discuss-deploy"){
    dependencies {
        compile project(':discuss-server:discuss-api')
    }
}

project(":discuss-common:discuss-common-entity"){
    dependencies {
        compile("org.hibernate.javax.persistence:hibernate-jpa-2.1-api:${hibernateJpaVersion}")
    }
}

configure(subprojects.findAll { it.name.contains('deploy')}) {
    apply plugin: 'org.springframework.boot'
}